group 'com.spark'
version '1.0.0'

apply plugin: 'java'
apply plugin: 'scala'
apply plugin: 'application' //可选(可自动生成shell启动脚本)
mainClassName = 'com.spark.realTimePv.PvCalculate' //可选（与上行的application插件配套出现）

sourceCompatibility = 1.8


repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
}

task "createDirs" << {
    sourceSets*.scala.srcDirs*.each { it.mkdirs() }
    sourceSets*.java.srcDirs*.each { it.mkdirs() }
    sourceSets*.resources.srcDirs*.each { it.mkdirs() }
}

def env = System.getProperty("profile") ?: "dev"

sourceSets {
    main {
        scala {
            srcDirs = ['src/main/scala', 'src/main/java']
        }
        java {
            srcDirs = []
        }
        resources {
            srcDirs = ["src/main/resources", "src/main/config/$env"]
        }
    }

    test {
        scala {
            srcDirs = ['src/test/scala', 'src/test/java']
        }
        java {
            srcDirs = []
        }
        resources {
            srcDirs = ["src/main/resources", "src/main/filters/$env"]
        }
    }
}

dependencies {
    compile "org.scala-lang:scala-library:2.11.11"
    compile "org.scala-lang:scala-compiler:2.11.11"
    compile "org.scala-lang:scala-reflect:2.11.11"
    compile "org.apache.spark:spark-core_2.11:2.1.1"
    compile "org.apache.spark:spark-streaming_2.11:2.1.1"
    compile "org.apache.spark:spark-streaming-kafka-0-8_2.11:2.1.1"
    compile group: 'org.apache.kafka', name: 'kafka_2.11', version: '0.8.2.1'
    compile group: 'org.apache.spark', name: 'spark-sql_2.11', version: '2.1.1'
    compile group: 'org.apache.spark', name: 'spark-hive_2.11', version: '2.1.1'
    compile group: 'mysql', name: 'mysql-connector-java', version: '5.1.34'
    compile group: 'org.apache.hbase', name: 'hbase', version: '1.3.1'
    compile group: 'org.apache.hbase', name: 'hbase-client', version: '1.3.1'
    compile group: 'c3p0', name: 'c3p0', version: '0.9.1.2'
    compile group: 'com.typesafe', name: 'config', version: '1.2.1'
    compile group: 'log4j', name: 'log4j', version: '1.2.17'
    testCompile group: 'junit', name: 'junit', version: '4.12'
}


jar {
    zip64 true
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    exclude 'META-INF/*.RSA', 'META-INF/*.SF', 'META-INF/*.DSA'
    manifest {
        attributes "Main-Class": "com.spark.realTimePv.PvCalculate"
    }
}

